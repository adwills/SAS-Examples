/*PULL DATA FROM https://www.countyhealthrankings.org/explore-health-rankings/rankings-data-documentation/national-data-documentation-2010-2017*/

/*SPECIFY DIRECTORY IN WHICH TO SAVE FILES*/

/*********************************************************/
/**********CHANGE THIS LINE FOR YOUR ENVIRONMENT**********/
/*********************************************************/
%LET LOCATION=/r/ge.unx.sas.com/vol/vol620/u62/anddwi/CHR;
/*********************************************************/

LIBNAME CHR "&LOCATION";

/*CREATE LOOKUP TABLE FOR MSA CODES*/
PROC SQL;
	CREATE TABLE MSA AS SELECT
	DISTINCT STATE, COUNTY, MSA
	FROM SASHELP.ZIPCODE;
QUIT;

%MACRO CHRPULL(YEAR=);
FILENAME CH&YEAR "&LOCATION.CHR&YEAR..csv";

/*DOWNLOAD CSV*/
PROC HTTP 
	URL="https://www.countyhealthrankings.org/sites/default/files/analytic_data&YEAR..csv"
	METHOD= "GET" 
	OUT=CH&YEAR;
RUN;

/*IMPORT TO SAS*/
PROC IMPORT OUT= CHR&YEAR._RAW
            DATAFILE= "&LOCATION.CHR&YEAR..csv" 
            DBMS=CSV REPLACE;
     GETNAMES=NO;
     DATAROW=2; 
	 GUESSINGROWS=1000; 
RUN;

/*GET VARIABLE NAMES FROM FIRST ROW*/
DATA VARNAMES&YEAR CHR&YEAR;
	SET CHR&YEAR._RAW;
	IF _N_ = 1 THEN OUTPUT VARNAMES&YEAR;
	ELSE OUTPUT CHR&YEAR;
RUN;

/*CREATE MAPPING OF VARIABLE NAMES TO AUTOMATICALLY-GENERATED COLUMN NAMES*/
PROC TRANSPOSE DATA=VARNAMES&YEAR OUT=NAMETRAN&YEAR;
	VAR VAR:;
RUN;

PROC SQL NOPRINT;
	SELECT COL1 INTO :VARNAMES SEPARATED BY " " FROM NAMETRAN&YEAR WHERE _NAME_ ^IN("VAR1","VAR2","VAR3","VAR4","VAR5");
	SELECT _NAME_ INTO :VARNUMS SEPARATED BY " " FROM NAMETRAN&YEAR WHERE _NAME_ ^IN("VAR1","VAR2","VAR3","VAR4","VAR5");
QUIT;

%PUT &VARNAMES;
%PUT &VARNUMS;

/*CHANGE TO CORRECT VARIABLE NAMES*/
DATA CHR&YEAR;
	SET CHR&YEAR;
	LENGTH STATECODE COUNTYCODE 8 FIPSCODE $5 STATE $2 COUNTY $43;
	STATECODE = INPUT(VAR1,8.);
	COUNTYCODE = INPUT(VAR2,8.);
	FIPSCODE = VAR3;
	STATE = VAR4;
	COUNTY = VAR5;
	ARRAY VARNAMES{*} &VARNAMES;
	ARRAY VARNUMS{*} &VARNUMS;
	DO I = 1 TO DIM(VARNAMES);
		VARNAMES{I} = INPUT(VARNUMS{I},8.);
	END;
	DROP I VAR:;
RUN;

PROC SQL;
	CREATE TABLE CHR.CHR&YEAR
	AS SELECT A.*, B.MSA
	FROM CHR&YEAR A
	LEFT JOIN
	MSA B
	ON A.STATECODE = B.STATE
	AND A.COUNTYCODE = B.COUNTY;
QUIT;

%MEND CHRPULL;

%CHRPULL(YEAR=2017);

/*START A CAS SESSION, WHICH WILL AUTOMATICALLY BE GIVEN THE NAME CASAUTO*/
CAS;

/*SHOW CASLIBS IN SAS STUDIO LIBRARY TAB*/
CASLIB _ALL_ ASSIGN;

/*LOAD DATA TO MEMORY*/
PROC CASUTIL;
	LOAD DATA=CHR.CHR2017 OUTCASLIB="CASUSER" CASOUT="CHR2017" PROMOTE;
QUIT;

/*TERMINATE THE CAS SESSION*/
CAS CASAUTO TERMINATE;
