/*PULL DATA FROM https://www.countyhealthrankings.org/explore-health-rankings/rankings-data-documentation/national-data-documentation-2010-2017*/
%LET LOCATION=C:\Users\anddwi\Andrew\;

LIBNAME CHR "&LOCATION";

%MACRO CHRPULL(YEAR=);

FILENAME CH&YEAR "&LOCATION.CHR&YEAR..sas7bdat";

PROC HTTP 
	URL="https://www.countyhealthrankings.org/sites/default/files/analytic_data&YEAR..sas7bdat"
	METHOD= "GET"
	OUT=CH&YEAR;
RUN;

%MEND CHRPULL;

%CHRPULL(YEAR=2010);
/*%CHRPULL(YEAR=2011);*/
%CHRPULL(YEAR=2012);
%CHRPULL(YEAR=2013);
%CHRPULL(YEAR=2014);
%CHRPULL(YEAR=2015);
%CHRPULL(YEAR=2016);
%CHRPULL(YEAR=2017);
%CHRPULL(YEAR=2017);
%CHRPULL(YEAR=2018_0);
%CHRPULL(YEAR=2019);

/*SPECIAL CASE FOR 2011 WHERE DATA IS ONLY AVAILABLE IN CSV FILE*/

FILENAME CH2011 "&LOCATION.CHR2011.csv";

/*DOWNLOAD CSV*/
PROC HTTP 
	URL="https://www.countyhealthrankings.org/sites/default/files/analytic_data2011.csv"
	METHOD= "GET" 
	OUT=CH2011;
RUN;

/*IMPORT TO SAS*/
PROC IMPORT OUT= CHR2011_RAW
            DATAFILE= "&LOCATION.CHR2011.csv" 
            DBMS=CSV REPLACE;
     GETNAMES=NO;
     DATAROW=2; 
	 GUESSINGROWS=1000; 
RUN;

/*GET VARIABLE NAMES FROM FIRST ROW*/
DATA VARNAMES CHR2011;
	SET CHR2011_RAW;
	IF _N_ = 1 THEN OUTPUT VARNAMES;
	ELSE OUTPUT CHR2011;
RUN;

/*CREATE MAPPING OF VARIABLE NAMES TO AUTOMATICALLY-GENERATED COLUMN NAMES*/
PROC TRANSPOSE DATA=VARNAMES OUT=NAMETRAN;
	VAR VAR:;
RUN;

PROC SQL NOPRINT;
	SELECT COL1 INTO :VARNAMES SEPARATED BY " " FROM NAMETRAN WHERE _NAME_ ^IN("VAR1","VAR2","VAR3","VAR4","VAR5");
	SELECT _NAME_ INTO :VARNUMS SEPARATED BY " " FROM NAMETRAN WHERE _NAME_ ^IN("VAR1","VAR2","VAR3","VAR4","VAR5");
QUIT;

%PUT &VARNAMES;
%PUT &VARNUMS;

/*CHANGE TO CORRECT VARIABLE NAMES*/
DATA CHR.CHR2011;
	SET CHR2011;
	LENGTH STATECODE $2 COUNTYCODE $3 FIPSCODE $5 STATE $2 COUNTY $43;
	STATECODE = VAR1;
	COUNTYCODE = VAR2;
	FIPSCODE = VAR3;
	STATE = VAR4;
	COUNTY = VAR5;
	ARRAY VARNAMES{*} &VARNAMES;
	ARRAY VARNUMS{*} &VARNUMS;
	DO I = 1 TO DIM(VARNAMES);
		VARNAMES{I} = INPUT(VARNUMS{I},8.);
	END;
	DROP I VAR:;
RUN;

/*APPEND ALL YEARS OF DATA*/
DATA CHR.FULL_CHR;
	SET CHR.CHR:;
RUN;
